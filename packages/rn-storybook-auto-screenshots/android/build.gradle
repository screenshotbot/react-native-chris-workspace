buildscript {
    ext.safeExtGet = {prop, fallback ->
        rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.0.2")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${safeExtGet('kotlinVersion', '1.9.23')}")
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace "com.rnstorybookautoscreenshots"
    compileSdk safeExtGet('compileSdkVersion', 34)

    defaultConfig {
        minSdk safeExtGet('minSdkVersion', 24)
        targetSdk safeExtGet('targetSdkVersion', 34)
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation "com.facebook.react:react-android:+"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${safeExtGet('kotlinVersion', '1.9.23')}"

    // Test infrastructure dependencies - compileOnly so they're not bundled into consuming apps
    // Apps using the base test classes must add these to their own androidTestImplementation
    compileOnly "androidx.test.ext:junit:1.1.5"
    compileOnly "androidx.test:runner:1.5.2"
    compileOnly "androidx.test:rules:1.5.0"
    compileOnly "androidx.test:core:1.5.0"
    compileOnly "io.screenshotbot.screenshot-tests-for-android:core:1.0.1"
}
